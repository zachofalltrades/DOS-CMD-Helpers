@echo off
VER
ECHO/
ECHO ----- POWERSHELL
powershell -Command "$PSVersionTable"
ECHO ----- USERPROFILE
NET user /domain %USERNAME%
GPRESULT /user %USERNAME% /r
ECHO/
ECHO ----- ENVIRONMENT
SET
SET NOTINPATH=
SET AVAILABLE=
CALL :CHECKFOR java -version
CALL :CHECKFOR javac -version
CALL :CHECKFOR python -V
CALL :CHECKFOR git --version
CALL :CHECKFOR mvn --version
CALL :CHECKFOR ant --version
CALL :CHECKFOR gradle --version
CALL :CHECKFOR bash --version
CALL :CHECKFOR node --version
::CALL :CHECKFOR cleartool -version
::CALL :CHECKFOR rcleartool -version

CALL :CHECKFOR 7z
CALL :CHECKFOR procexp
CALL :CHECKFOR robocopy

ECHO/ 
ECHO/ 

ECHO ----- TOOL CHECK RESULTS
IF DEFINED AVAILABLE (
  ECHO AVAILABLE: %AVAILABLE%
)
IF DEFINED NOTINPATH (
  ECHO NOTINPATH: %NOTINPATH%
)
ECHO/ 
CALL :CHECKPATH HOME
CALL :CHECKPATH JAVA_HOME
CALL :CHECKPATH MAVEN_HOME
CALL :CHECKPATH ANT_HOME

SET NOTINPATH=
SET AVAILABLE=
SET TOCHECK=
SET VERSION=
ECHO/
EXIT /b

:CHECKFOR
SET TOCHECK=%1
SET VERSION=%2
ECHO/
ECHO ----- CHECKING FOR "%TOCHECK%"
WHERE %TOCHECK%
IF ERRORLEVEL 1 (
  SET NOTINPATH=%NOTINPATH% %TOCHECK%
  EXIT /b
)
SET AVAILABLE=%AVAILABLE% %TOCHECK%
IF "%VERSION%"=="" EXIT /b
ECHO %TOCHECK% %VERSION%
CALL %TOCHECK% %VERSION%
ECHO/
EXIT /b

:CHECKPATH
IF NOT DEFINED %1 (
  ECHO %1 is NOT DEFINED
  EXIT /b
)
SET _var=%1
CALL SET _val=%%%1%%%
IF NOT EXIST %_val% (
  SET _msg=BUT DIRECTORY DOES NOT EXIST
  CALL :PATHMSG
  EXIT /b
)
SET _bin=%_val%\bin;
IF NOT EXIST %_bin% (
  SET _msg=BUT \bin SUBDIRECTORY DOES NOT EXIST
  CALL :PATHMSG
  EXIT /b
)
CALL SET _chk=%%PATH:%_bin%=%%
IF NOT "%PATH%"=="%_chk%" (
 SET _msg=and \bin is on PATH
) ELSE (
 SET _msg=BUT \bin SUBDIRECTORY IS NOT ON PATH
)
CALL :PATHMSG
EXIT /b

:PATHMSG
ECHO %_var% = %_val% %_msg%
SET _var=
SET _val=
SET _bin=
SET _chk=
SET _msg=
EXIT /b
