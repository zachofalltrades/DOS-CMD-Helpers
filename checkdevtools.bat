@ECHO OFF & SETLOCAL
VER
ECHO/
ECHO ----- POWERSHELL
POWERSHELL -Command "$PSVersionTable"
ECHO ----- USERPROFILE
NET user /domain %USERNAME%
GPRESULT /user %USERNAME% /r
ECHO/
ECHO ----- ENVIRONMENT
SET
SET NOTINPATH=
SET AVAILABLE=
CALL :CHECK java -version JAVA_HOME
CALL :CHECK javac -version
CALL :CHECK python -V PYTHONPATH
CALL :CHECK git --version
::SET GIT_
CALL :CHECK mvn --version MAVEN_HOME
CALL :CHECK ant --version ANT_HOME
CALL :CHECK gradle --version GRADLE_HOME
CALL :CHECK bash --version
CALL :CHECK node --version
CALL :CHECK atom --version ATOM_HOME
::CALL :CHECK cleartool -version
::CALL :CHECK rcleartool -version
CALL :CHECK 7z
CALL :CHECK procexp
CALL :CHECK robocopy

ECHO/ 
ECHO/ 

ECHO ----- TOOL CHECK RESULTS
CALL :CHECKVARS %PATHVARS%
ECHO/ 
ECHO/ 
IF DEFINED AVAILABLE (
  ECHO AVAILABLE: %AVAILABLE%
)
IF DEFINED NOTINPATH (
  ECHO NOTINPATH: %NOTINPATH%
)
ECHO/ 
EXIT /b

:CHECK
SET PATHVARS=%PATHVARS% %3
ECHO/
ECHO ----- CHECKING FOR "%1"
WHERE %1
IF ERRORLEVEL 1 (
  SET NOTINPATH=%NOTINPATH% %1
  EXIT /b
)
SET AVAILABLE=%AVAILABLE% %1
IF "%2"=="" EXIT /b
ECHO %1 %2
CALL %1 %2
ECHO/
EXIT /b

:CHECKVARS
IF "%1"=="" EXIT /b
CALL :CHECKVAR %1
SHIFT
GOTO :CHECKVARS

:CHECKVAR
ECHO/
ECHO %1
IF NOT DEFINED %1 (
  ECHO    is NOT DEFINED
  EXIT /b
)
CALL SET _dir=%%%1%%%
ECHO    is %_dir%
IF NOT EXIST %_dir% (
  ECHO    BUT DIRECTORY DOES NOT EXIST
  EXIT /b
)
IF NOT EXIST %_dir%\bin (
  ECHO    BUT \bin SUBDIRECTORY DOES NOT EXIST
  EXIT /b
)
CALL SET _chk=%%PATH:%_dir%\bin=%%
IF NOT "%PATH%"=="%_chk%" (
  ECHO    and \bin subdirectory is on PATH
) ELSE (
 ECHO     BUT \bin SUBDIRECTORY IS NOT ON PATH
)
EXIT /b
